unit := itemList! "EOF"?

itemList := item itemList ||
item := import | class | method

import := "import"? importType! importName! dotWordChain!
importName := id | "*"?
importType := "@"? id! ||

class := "class"? id! extends! implements! classBlock!
extends := "extends"? type ||
implements := "implements"? typeList ||

classBlock := "{"? classItemList! "}"?!

classItemList := classItem classItemList! ||
classItem := method | field

field := id ":" type!


method := "fn"? id! "("?! parameterList! ")"?! returnTypeOpt! extends! "="! expression!
returnTypeOpt := "->"? type! ||
parameterList := parameter parameterListOpt! ||
parameterListOpt := "," parameterList! ||
parameter := id ":"?! type!

blockExpr := "{"? blockExprList!
blockExprList := "}"? | expression blockExprList!

expression := variableDefinition | factor
variableDefinition := "var" id! typeHint! "="! expression! |
typeHint := ":"? type! ||
branch := "if"? expression! body! branchOpt!
branchOpt := "else"? expression! ||
body := blockExpr!

factor := object postfixList! assignOpt!
assignOpt := "="? expression! ||

this := "this"
object := initialisation | branch | blockExpr | this | string | number | id | "("? expression! ")"?!

initialisation := "new"? type! initialisationList!
initialisationList := "{"? initialisationFieldList! "}"?! ||
initialisationFieldList := initialisationField initialisationFieldListOpt! ||
initialisationFieldListOpt := ","? initialisationFieldList! ||
initialisationField := expression

postfixList := postFix postfixList! ||
postFix := "."? id! | callAccess | cast | castCheck
callAccess := "("? expressionList! ")"?! | ":"? expression!
cast := "as"? type!
castCheck := "is"? type!

expressionList := expression expressionListOpt! ||
expressionListOpt := ","? expressionList! ||

type := arrayType | classType | primitive
primitive := "void" | "double" | "float" | "int" | "byte" | "short" | "long" | "boolean" | "char"
arrayType := "["? type! "]"?!
classType := dotWordChain

typeList := type typeListOpt! ||
typeListOpt := ","? typeList! ||

dotWordChain := id dotWordChainOpt!
dotWordChainOpt := "."? dotWordChain ||

superCall := "super"? "<"? type! ">"?! id! "("?! expressionList! ")"?! | "super" "("? expressionList! ")"?!


id := "word"
number := "number"
string := "string"


function := "fn"? "("?! functionParameterList! ")"?! returnTypeOpt! functionType! "="?! expression!
functionType := "implements" type! ||

functionParameterList := functionParameter functionParameterListOpt! ||
functionParameterListOpt := "," functionParameterList! ||
functionParameter := id ":"? type! | id


ConditionalOrExpression := ConditionalAndExpression ConditionalOrExpressionRight!
ConditionalOrExpressionRight := ConditionalOrExpressionFunction ConditionalOrExpression ||
ConditionalOrExpressionFunction := "||"

ConditionalAndExpression := InclusiveOrExpression ConditionalAndExpressionRight!
ConditionalAndExpressionRight := ConditionalAndExpressionFunction ConditionalAndExpression ||
ConditionalAndExpressionFunction := "&&"

InclusiveOrExpression := ExclusiveOrExpression InclusiveOrExpressionRight!
InclusiveOrExpressionRight := InclusiveOrExpressionFunction InclusiveOrExpression ||
InclusiveOrExpressionFunction := "|"

ExclusiveOrExpression := AndExpression ExclusiveOrExpressionRight!
ExclusiveOrExpressionRight := ExclusiveOrExpressionFunction ExclusiveOrExpression ||
ExclusiveOrExpressionFunction := "^"

AndExpression := EqualityExpression AndExpressionRight!
AndExpressionRight := AndExpressionFunction AndExpression ||
AndExpressionFunction := "&"

EqualityExpression := RelationalExpression EqualityExpressionRight!
EqualityExpressionRight := EqualityExpressionFunction EqualityExpression ||
EqualityExpressionFunction := "==" | "!="

RelationalExpression := AdditiveExpression RelationalExpressionRight!
RelationalExpressionRight := RelationalExpressionFunction RelationalExpression ||
RelationalExpressionFunction := ">=" | "<=" | ">" | "<"

AdditiveExpression := MultiplicativeExpression AdditiveExpressionRight!
AdditiveExpressionRight := AdditiveExpressionFunction AdditiveExpression ||
AdditiveExpressionFunction := "+" | "-"

MultiplicativeExpression := UnaryExpression MultiplicativeExpressionRight!
MultiplicativeExpressionRight := MultiplicativeExpressionFunction MultiplicativeExpression ||
MultiplicativeExpressionFunction := "*" | "/" | "%"

UnaryExpression := "!" factor | "~" factor | "-" factor | "+" factor | factor


//boolArithmetic := comparisonArithmetic boolRight!
//comparisonArithmetic := arithmetic comparisonRight!
//arithmetic := term arithmeticRight!
//term := factor termRight!
//
//boolRight := boolArithmeticFunc boolArithmetic ||
//comparisonRight := comparisonArithmeticFunc comparisonArithmetic ||
//arithmeticRight := arithmeticFunc arithmetic ||
//termRight := termFunc term ||
//
//factor := object memberList! assignOpt!

////operators
//termFunc := "*" | "/"
//arithmeticFunc := "+" | "-"
//comparisonArithmeticFunc := ">" | ">=" | "<" | "<=" | "==" | "!="
//boolArithmeticFunc := "&&" | "||"
